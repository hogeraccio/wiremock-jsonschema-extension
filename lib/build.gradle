plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.32.0'

    shadow 'com.github.tomakehurst:wiremock-jre8:2.32.0'
    implementation 'com.networknt:json-schema-validator:1.0.66'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
}

group = 'com.github.hogeraccio'
jar.archiveBaseName = rootProject.name
version = '0.1'

tasks.named('test') {
    useJUnitPlatform()
}

shadowJar {
    classifier = ''
    archiveBaseName = rootProject.name

    relocate "com.fasterxml.jackson.core", 'hogeraccio.com.fasterxml.jackson.core'
    relocate "com.networknt", 'hogeraccio.com.networknt'
}

jar.finalizedBy shadowJar

java {
    withSourcesJar()
    withJavadocJar()
}

def renameArtifact(classifier) {
    def dir = "${buildDir}/${libsDirName}"
    def filter = "${project.name}-*"
    def src = "${project.name}-(.+)"
    def dst = "${rootProject.name}-\$1"
    copy {
        from dir
        into dir
        include filter

        rename src, dst
    }
    delete fileTree(dir) { include filter }
}

task renameArtifacts {
    doLast {
        renameArtifact('javadoc')
        renameArtifact('sources')
    }
}

assemble.finalizedBy renameArtifacts

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/hogeraccio/wiremock-jsonschema-extension")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
